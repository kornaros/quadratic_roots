<!DOCTYPE html>
<html lang="el">
<head>
  <meta charset="UTF-8">
  <title>kornaros_demo_for_quadratic_roots</title>
  <script src="https://sagecell.sagemath.org/static/jquery.min.js"></script>
  <script src="https://sagecell.sagemath.org/embedded_sagecell.js"></script>
  <script>
    $(function () {
      sagecell.makeSagecell({
        inputLocation: '#sage-interact',
        template: sagecell.templates.interact,
        evalButtonText: '',
        autoeval: true,
        hide: ['editor', 'evalButton']
      });
    });
  </script>
</head>
<body style="font-family: Arial, sans-serif; margin: 2em; max-width: 800px;">
  <h2>Διαδραστικό Demo – Λύση Δευτεροβάθμιας Εξίσωσης</h2>
  <p style="font-size: 1.1em; color: #333;">
    Αυτό είναι ένα διαδραστικό demo των δυνατοτήτων του <strong>SageMath</strong>.
    Μπορείτε να υπολογίσετε τις ρίζες μιας δευτεροβάθμιας εξίσωσης εισάγοντας τιμές για τους συντελεστές <strong>a, b, c</strong> στο διάστημα <code>[-10, 10]</code>.
    Οι τιμές μπορούν να περιλαμβάνουν μαθηματικές παραστάσεις όπως:
    <code>π</code>, <code>e</code>, <code>sqrt(2)</code> (τετραγωνική ρίζα του 2), κ.ά.
    <br><br>
    Απλώς εισάγετε τιμές και πατήστε <strong>Enter</strong> ή αλλάξτε πεδίο για να εκτελεστεί ο υπολογισμός.
  </p>

  <div id="sage-interact">
    <script type="text/x-sage">
from sage.all import *
x = var('x')

def format_expr(expr):
    s = latex(expr)
    if any(token in s for token in [r'+', r'-', r'\cdot', r'\,', r'\frac']):
        s = s.replace(r'\,', ' ')
        return r"\left(" + s + r"\right)"
    else:
        return s

@interact
def solve_quadratic(
    a_str = input_box('1', label='a', type=str),
    b_str = input_box('1', label='1', type=str),
    c_str = input_box('1', label='c', type=str)
):
    try:
        a = SR(a_str)
        b = SR(b_str)
        c = SR(c_str)

        a_val = a.n()
        b_val = b.n()
        c_val = c.n()

        if not (-10 <= a_val <= 10 and -10 <= b_val <= 10 and -10 <= c_val <= 10):
            pretty_print(html("<span style='color:red'>Οι τιμές πρέπει να είναι μεταξύ -10 και 10.</span>"))
            return

        eq = a*x^2 + b*x + c == 0
        Delta = b^2 - 4*a*c
        Delta_val = Delta.n()

        show(LatexExpr(r"\text{Λύνουμε την εξίσωση:} \quad %s x^2 + %s x + %s = 0" % (
            format_expr(a), format_expr(b), format_expr(c))))
        show(LatexExpr(r"\Delta = b^2 - 4ac = %s" % latex(Delta)))

        solutions = solve(eq, x)
        plot1 = plot(a*x^2 + b*x + c, (x, -10, 10), ymin=-10, ymax=10, color='blue', thickness=2, legend_label="y = ax² + bx + c")

        if Delta_val > 0:
            roots = [sol.rhs() for sol in solutions]
            for i, r in enumerate(roots):
                approx = r.n(digits=3)
                symbol = "=" if r == approx else r"\approx"
                label = r"$x_%d %s %s$" % (i+1, symbol, latex(approx))
                plot1 += point((r.n(), 0), size=30, color='green', legend_label=label)
            root_latex = r",\quad ".join([latex(r) for r in roots])
            show(LatexExpr(r"\textcolor{green}{\text{Πραγματικές ρίζες: }} " + root_latex))

        elif Delta_val == 0:
            r = solutions[0].rhs()
            approx = r.n(digits=3)
            symbol = "=" if r == approx else r"\approx"
            label = r"$x %s %s$" % (symbol, latex(approx))
            plot1 += point((r.n(), 0), size=30, color='green', legend_label=label)
            show(LatexExpr(r"\textcolor{green}{\text{Διπλή ρίζα: }} " + latex(r)))

        else:
            root_latex = r",\quad ".join([latex(sol.rhs()) for sol in solutions])
            show(LatexExpr(r"\textcolor{red}{\text{Μη πραγματικές ρίζες: }} " + root_latex))

        plot1.show(legend_loc='upper right')

    except Exception as e:
        pretty_print(html("<span style='color:red'>Σφάλμα στην είσοδο: παρακαλώ συμπληρώστε σωστά τον μαθηματικό τύπο.</span>"))
    </script>
  </div>
</body>
</html>
